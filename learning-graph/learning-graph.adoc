= Learning Graph

This graph is used to visualize the knowledge one has in a certain area. 
In this example, the knowledge a Java developer has over a stack of technologies.
The purpose is to document acquired knowledge and also to help to further educate oneself in a structured way.
This is accomplished by graphing dependencies between technologies as well as resources that can be used to learn a technology
and to determine possible 'learning paths' through the graph, which show a way to learn a specific technology, by first
learning the technologies, in order, which are prerequisites for the desired technology.
This graph is meant not to be static, but updated as new connections between technologies are found and new knowledge is aquired.

The main use case explored here is how to learn Spring Data Neo4j (SDN) in a structured manner.

==== Node types
* Person: green
* Technology: yellow
* Concept: orange
* Resources: blue-ish (Book, Tutorial, Manual)

=== The graph at the beginning

image::https://github.com/jotomo/neo4j-gist-challenge/raw/master/learnng-graph/learning-graph-start.png[]

//hide
//setup
[source,cypher]
----
//// Create nodes

// Technologies
CREATE 
  (java:Tech { name:"Java" }),
  (spring:Tech { name:"Spring" }),
  (neo4j:Tech { name:"Neo4j" }),
  (springDataNeo4j:Tech { name:"Spring Data Neo4j" })

// Concepts  
CREATE 
  (dependencyInjection:Concept { name:"Dependency Injection" })

// Resources
CREATE  
  (hfj:Book { name:"Head First Java" }),
  (effectiveJava:Book { name:"Effective Java", level:"ADVANCED" }),
  (neo4jTutorial:Tutorial { name:"Neo4j Tutorial" }),
  (neo4jDocs:Manual { name:"Neo4j Reference Documentation" }),
  (neo4jInAction:Book { name:"Neo4j in Action" }),
  (springData:Book { name:"Spring Data" })
  
// People
CREATE
  (javaDev:Person { name:"Java Developer" }),
  (springExpert:Person { name:"Spring Expert" })
  

//// Relate nodes

// Dependencies
CREATE
  (springDataNeo4j)-[:REQUIRES]->(spring),
  (springDataNeo4j)-[:REQUIRES]->(neo4j),
  (spring)-[:REQUIRES]->(java),
  (spring)-[:REQUIRES]->(dependencyInjection)
  
// Existing knowledge
CREATE 
  (javaDev)-[:UNDERSTANDS { since:2013, level:"BASIC" }]->(java),
  (javaDev)-[:UNDERSTANDS { level:"BASIC" }]->(dependencyInjection),
  (springExpert)-[:UNDERSTANDS { since:2005 }]->(java),
  (springExpert)-[:UNDERSTANDS { since:2008 }]->(dependencyInjection),
  (springExpert)-[:UNDERSTANDS { since:2009, level:"EXPERT" }]->(spring)

// Paths to knowledge
CREATE
  (hfj)-[:TEACHES { level:"BASIC" }]->(java),
  (effectiveJava)-[:TEACHES { level:"ADVANCED" }]->(java),
  (neo4jTutorial)-[:TEACHES { level:"BASIC" }]->(neo4j),
  (neo4jDocs)-[:TEACHES]->(neo4j),
  (neo4jInAction)-[:TEACHES]->(neo4j),
  (springData)-[:TEACHES]->(springDataNeo4j)
----

Lets see what can be done this with data ...

=== What prior knowledge is required before learning Spring Data Neo4j?
[source,cypher]
----
START sdn=node:node_auto_index(name="Spring Data Neo4j")
MATCH p=(sdn)-[:REQUIRES*]->(required) 
RETURN last(p).name AS `Technology or concept`, length(p) AS `Steps to SDN` 
ORDER BY `Steps to SDN` DESC
----
//table

=== Visualization of possible learning paths
//hide
[source,cypher]
----
MATCH p=(sdn)-[:REQUIRES*]->(required)   
RETURN p AS possiblePath
----
//graph

=== What does the Java Developer know already?
[source,cypher]
----
START javaDev=node:node_auto_index(name="Java Developer") 
MATCH (javaDev)-[:UNDERSTANDS]->(tech) 
RETURN tech.name AS `Technology or concept`
----
//table

== Which resources can be used to get an overview of the technologies?
This query favors resources with a _basic_ (or beginner) level to allow to quickly learn about the technologies, what they consist of, how much time they might need to learn etc, without going too much into detail of actually using the technology.
[source,cypher]
----
START sdn=node:node_auto_index(name="Spring Data Neo4j")
MATCH p=(sdn)-[:REQUIRES*0..]->(technology:Tech)<-[teaches:TEACHES]-(resource)
WHERE not(has(teaches.level)) OR teaches.level = "BASIC"
RETURN collect(resource.name) AS Resource, technology.name AS Technology, length(p) AS `Steps to SDN` 
ORDER BY `Steps to SDN` DESC
----
//table

== Add newly acquired knowledge about technology relations to the graph
Now, having a good overview of the technologies, the understanding - as represented by the graph - can be extended. E.g. now it is known that SDN has two modes and that SDN requires understanding of the Repository Pattern. Furthermore it was learned that Aspect-oriented programming is a concept that is a part of Spring (but not a requirement for Spring, nor SDN, as it is optional).

[source,cypher]
----
START 
  spring=node:node_auto_index(name="Spring"), 
  sdn=node:node_auto_index(name="Spring Data Neo4j"), 
  springDataBook=node:node_auto_index(name="Spring Data")
CREATE
  (sdn)-[:REQUIRES]->(repositoryPattern:Concept {name: "Repository Pattern"}),
  (springDataBook)-[:TEACHES]->(repositoryPattern),
  (sdn)<-[:PART_OF]-(simpleMode:Tech {name:"Simple Mapping Mode"}),
  (sdn)<-[:PART_OF]-(advancedMode:Tech {name:"Advanced Mapping Mode"}),
  (simpleMode)<-[:ALTERNATIVE_TO]->(advancedMode),
  (spring)-[:USES]->(aop:Concept {name:"Aspect-Oriented Programming"}),
  (simpleMode)<-[:REJECTED]-(eval:Evaluation {name:"SDN Modes Evaluation"})-[:ACCEPTED]->(advancedMode)
----

Additionally, while learning about SDN, an _Evaluation_ of the modes was done and a decision was made to use the advanced mapping mode, which was documented by the newly created Evaluation node.

The graph would then look like this (new nodes are displayed with an ellipse shape)

image::https://github.com/jotomo/neo4j-gist-challenge/raw/master/learnng-graph/learning-graph-end.png[]

Even further, an evaluation of the modes depends on whether an embedded database or a remote database is used, which should be decided on a per-project basis. So the project could be modeled in the graph too, get connected to the evaluation node and then, of course, get connected to the technologies the project uses. Then we can ask the graph further questions, like "Which projects has the Java Developer worked on and which technologies has he used during?". As this example is about learning, modeling projects is out of scope and hence omitted.

However, the fact that Spring uses Aspect-Oriented Programming was learned rather by accident, through a Google search. There is no resource to learn Spring in the graph! This can't be good. Are there any more?
[source,cypher]
----
START tech=node(*) 
MATCH (tech)<-[r?:TEACHES]-(resource)
WHERE r is null AND (tech:Tech OR tech:Concept)  
RETURN tech.name as Technology
----
//table
There it is, no resource for learning Spring. Nor for Dependency Injection or SDNs modes. That's okay for the Java Developer though, as he knows Dependency Injection, and the modes are a part of SDN, so they're explained in the Spring Data book.

=== What resources are available to the Java Developer to deep-dive Spring Data Neo4j and required technologies and concepts, skipping what he already knows?
[source,cypher]
----
START sdn=node:node_auto_index(name="Spring Data Neo4j"),
      javaDev=node:node_auto_index(name="Java Developer")
MATCH p=(sdn)-[:REQUIRES*0..]->(technologyOrConcept)<-[teaches:TEACHES]-(resource)
WHERE NOT( (javaDev)-[:UNDERSTANDS]->(technologyOrConcept) ) OR teaches.level <> 'BASIC'
RETURN technologyOrConcept.name AS `Technology or concept`, collect(DISTINCT resource.name) AS Resource
----
//table

=== What resources are available to the Java Developer to master existing skills?
[source,cypher]
----
START javaDev=node:node_auto_index(name="Java Developer")
MATCH (javaDev)-[:UNDERSTANDS]->(technology:Tech)<-[:TEACHES]-(resource)
WHERE resource.level = "ADVANCED"
RETURN technology.name as Technology, resource.name AS Resource
----
//table

=== How many years of experience does the Spring Expert have with each technology?
[source,cypher]
----
START expert=node:node_auto_index(name="Spring Expert") 
MATCH (expert)-[understands:UNDERSTANDS]->(tech:Tech) 
RETURN tech.name AS Technology,(2013 - understands.since) AS `Years of experience`
----
//table

Dependency Injection is something the Spring Expert understands to, but it's a Concept, not a Technology and this query was restricted to nodes with a Technology label.

=== Who can be asked for help on a specific technology?
[source,cypher]
----
MATCH (tech:Tech)<-[:UNDERSTANDS]-(person:Person)
RETURN tech.name AS Technology, collect(DISTINCT person.name) AS Person
----
//table

With a bigger graph - maybe when graphing the knowledge of several team members of a project - new possibilities arise: it could become possible to run queries to find 'enabler nodes', which are a prerequisite for multiple technologies and would therefore be well suited to broaden one's understanding and knowledge.

=== Console to play around with the graph
//console

//graph
